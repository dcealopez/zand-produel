// Chaingun
ACTOR ChaingunE : Chaingun
{
	AttackSound "weapons/chngun"
	Decal BulletChip
	Weapon.SlotNumber 4
	Obituary "%k \cc> \ciChaingun \cc> %o"
	-DROPPED
	Inventory.RespawnTics 125
	States
	{
		Deselect:
			CHGG A 0 A_Lower
			CHGG A 0 A_Lower
			CHGG A 1 A_Lower
			Loop	 

		Select:
			CHGG A 0 A_StopSound(7)
			CHGG A 0 A_PlaySound("wpncg", 7, 1, 0, 1.2)
			
		SelectLoop:
			CHGG A 0 A_Raise
			CHGG A 0 A_Raise
			CHGG A 1 A_Raise
			Loop

		Fire:	  
			TNT1 A 0 ACS_NamedExecuteAlways("Register Weapon Shot", 0, 5)
			TNT1 A 0 A_GunFlash("Flash3")
			CHGG A 2 Bright A_FireBullets(1.5, 0, -1, (9), "BulletPuff", FBF_USEAMMO | FBF_NORANDOM)
			TNT1 A 0 A_GunFlash("Flash4")
			CHGG B 2 Bright	  
			CHGG B 1 A_ReFire
			Goto Ready

		Flash3:
			CHGF A 4 Bright A_Light1
			Goto LightDone

		Flash4:
			CHGF B 4 Bright A_Light2
			Goto LightDone

		Spawn:
			TNT1 A 0
			TNT1 A 0 ACS_NamedExecuteAlways("Begin_Rotate")
			MGUN A 10
			Loop

		Rotate:
			TNT1 A 0 ACS_NamedExecuteAlways("End_Rotate")
			RTCG A 3 A_SetAngle(angle + 22.5)
			Loop

		NoRotate:
			TNT1 A 0 ACS_NamedExecuteAlways("Begin_Rotate")
			MGUN A 3
			Loop
	}
}

// Rocket Launcher Pickup
ACTOR PChaingun : CustomInventory Replaces Chaingun
{
	+INVENTORY.IGNORESKILL
	+INVENTORY.ALWAYSPICKUP
	-DROPPED
	Inventory.RespawnTics 125
	Inventory.PickupMessage "$GOTCHAINGUN"
	Inventory.PickupSound "misc/w_pkup"
	States
	{
		Spawn:
			TNT1 A 0
			TNT1 A 0 ACS_NamedExecuteAlways("Begin_Rotate")
			MGUN A 10
			Loop			
		Rotate:
			TNT1 A 0 ACS_NamedExecuteAlways("End_Rotate")
			RTCG A 3 A_SetAngle(angle + 22.5)
			Loop
		NoRotate:
			TNT1 A 0 ACS_NamedExecuteAlways("Begin_Rotate")
			MGUN A 3
			Loop			
		Pickup:
			TNT1 A 0
			TNT1 A 0 A_JumpIfInventory("ChaingunE", 1, "Repickup")
			TNT1 A 0 A_JumpIfInventory("PistolE", 1, "Repickup")	
			TNT1 A 0 A_GiveInventory("ChaingunE", 1)
			TNT1 A 0 A_TakeInventory("Clip", 999)
			TNT1 A 0 ACS_NamedExecuteAlways("Give Weapon Ammo", 0, 5, 100)
			Stop
		Repickup:
			TNT1 A 0
			TNT1 A 0 ACS_NamedExecuteAlways("Give Weapon Ammo", 0, 5, 50)
			TNT1 A 0 A_JumpIfInventory("ChaingunE", 1, 3)
			TNT1 A 0 A_GiveInventory("ChaingunE", 1)
			TNT1 A 0 A_TakeInventory("Clip", 20)
			TNT1 A 0
			Stop
	}
}

// Dropped Chaingun
ACTOR DChaingunE : PChaingun
{
	States
	{
		Spawn:
			MGUN A 175
		SpawnLoop:		
			MGUN A 7 A_FadeOut
			Loop
	}
}