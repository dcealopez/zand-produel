// BFG
ACTOR BFGE : BFG9000 Replaces BFG9000
{
	Weapon.AmmoType "BFGCELLPRO"	
	Weapon.AmmoGive 160
	Weapon.SlotNumber 7
	+INVENTORY.ALWAYSPICKUP
	-DROPPED
	Inventory.RespawnTics 2100
	Obituary "$OB_MPBFG_SPLASH"
	States
	{
		Deselect:
			BFGG A 0 A_Lower
			BFGG A 0 A_Lower
			BFGG A 1 A_Lower
			Loop	 

		Select:
			BFGG A 0 A_StopSound(7)
			BFGG A 0 A_PlaySound("wpncg", 7, 1, 0, 1.2)
			
		SelectLoop:	  
			BFGG A 0 A_Raise
			BFGG A 0 A_Raise
			BFGG A 1 A_Raise
			Loop

		Fire:
			BFGG A 20 A_BFGSound
			BFGG B 10 A_GunFlash
			BFGG B 10 A_FireBFG
			BFGG B 20 A_ReFire
			Goto Ready

		Spawn:
			TNT1 A 0
			TNT1 A 0 ACS_NamedExecuteAlways("Begin_Rotate")
			BFUG A 10
			Loop

		Rotate:
			TNT1 A 0 ACS_NamedExecuteAlways("End_Rotate")
			RTBF A 3 A_SetAngle(angle + 22.5)
			Loop

		NoRotate:
			TNT1 A 0 ACS_NamedExecuteAlways("Begin_Rotate")
			BFUG A 3
			Loop
	}
}

// BFG Ammo
ACTOR BFGCELLPRO : Ammo
{
	Inventory.PickupMessage "$GOTCELL"
	Inventory.Amount 40
	Inventory.MaxAmount 160
	Ammo.BackpackAmount 80
	Ammo.BackpackMaxAmount 160
	Inventory.Icon "CELLA0"
	States
	{
		Spawn:
			CELL A -1
			Stop
	}
}

// BFG Ball
ACTOR BFGBallE : BFGBall Replaces BFGBall
{
	Obituary "%k \cc> \ciBFG \cc> %o"
	States
	{
		Spawn:
			BFS1 AB 4 Bright
			Loop

		XDeath:
			TNT1 A 0 ACS_NamedExecute("Hit Marker Sound", 0, 1, 2)
			
		Death:
			BFE1 AB 8 Bright
			BFE1 C 8 Bright A_BFGSpray("NBFGExtra")
			BFE1 DEF 8 Bright
			Stop
	}
}

// BFG Tracer
ACTOR NBFGExtra : BFGExtra replaces BFGExtra
{
	+PUFFGETSOWNER
	States
	{
		Spawn:
			TNT1 A 0
			TNT1 A 0 ACS_NamedExecute("Hit Marker Sound", 0, 1, 1, 11)
			BFE2 ABCD 8 Bright			
			Stop
	}
}