// PlasmaRifle
ACTOR PlasmaRifleE : PlasmaRifle
{
	Weapon.SlotNumber 6
	Weapon.AmmoGive 20
	+NOAUTOAIM
	-DROPPED
	Inventory.RespawnTics 125
	Obituary "%k \cc> \ciPlasma Rifle \cc> %o"
	States
	{
		Deselect:
			PLSG A 0 A_Lower
			PLSG A 0 A_Lower
			PLSG A 1 A_Lower
			Loop

		Select:	  
			PLSG A 0 A_StopSound(7)
			PLSG A 0 A_PlaySound("wpncg", 7, 1, 0, 1.2)

		SelectLoop:
			PLSG A 0 A_Raise
			PLSG A 0 A_Raise
			PLSG A 1 A_Raise
			Loop	

		Fire:
			TNT1 A 0
			TNT1 A 0 ACS_NamedExecuteAlways("Register Weapon Shot", 0, 6)
			PLSG A 3 A_FirePlasma
			PLSG A 3 A_ReFire
			Goto Ready

		Spawn:
			TNT1 A 0
			TNT1 A 0 ACS_NamedExecuteAlways("Begin_Rotate")
			PLAS A 10
			Loop

		Rotate:
			TNT1 A 0 ACS_NamedExecuteAlways("End_Rotate")
			RTPS A 3 A_SetAngle(angle + 22.5)
			Loop

		NoRotate:
			TNT1 A 0 ACS_NamedExecuteAlways("Begin_Rotate")
			PLAS A 3
			Loop
	}
}

ACTOR PPlasmaRifle : CustomInventory replaces PlasmaRifle
{
	+INVENTORY.IGNORESKILL
	+INVENTORY.ALWAYSPICKUP
	-DROPPED
	Inventory.RespawnTics 125
	Inventory.PickupMessage "$GOTPLASMA"
	Inventory.PickupSound "misc/w_pkup"
	States
	{
		Spawn:
			TNT1 A 0
			TNT1 A 0 ACS_NamedExecuteAlways("Begin_Rotate")
			PLAS A 10
			Loop
		Rotate:
			TNT1 A 0 ACS_NamedExecuteAlways("End_Rotate")
			RTPS A 3 A_SetAngle(angle + 22.5)
			Loop
		NoRotate:
			TNT1 A 0 ACS_NamedExecuteAlways("Begin_Rotate")
			PLAS A 3
			Loop			
		Pickup:
			TNT1 A 0
			TNT1 A 0 A_JumpIfInventory("PlasmaRifleE", 1, "Repickup")
			TNT1 A 0 A_GiveInventory("PlasmaRifleE", 1)
			TNT1 A 0 A_TakeInventory("Cell", 999)
			TNT1 A 0 ACS_NamedExecuteAlways("Give Weapon Ammo", 0, 6, 50)
			Stop
		Repickup:
			TNT1 A 0
			TNT1 A 0 ACS_NamedExecuteAlways("Give Weapon Ammo", 0, 6, 25)
			Stop
	}
}

// Dropped Grenade Launcher
ACTOR DPlasmaRifleE : PPlasmaRifle
{
	States
	{
		Spawn:
			PLAS A 175
		SpawnLoop:		
			PLAS A 7 A_FadeOut
			Loop
	}
}

// Plasma Ball
ACTOR PlasmaBallE : PlasmaBall Replaces PlasmaBall
{
	+SEEKERMISSILE
	Decal PlasmaScorch
	Damage (random(2,6) * 5)
	Radius 6
	Height 3
	Speed 45
	Obituary "%k \cc> \ciPlasma Rifle \cc> %o"
	States
	{
		Death:
			TNT1 A 0 A_ChangeFlag("NOBLOCKMAP", 0)
			TNT1 A 0 A_ChangeFlag("NOINTERACTION", 1)
			TNT1 A 0 A_Stop
			TNT1 A 0 A_SeekerMissile(0, 0, SMF_LOOK | SMF_CURSPEED, 256)
			TNT1 A 0 A_JumpIfTargetInLOS("XDeathM", 360, JLOSF_PROJECTILE, 92)
			Goto DeathCont
			
		XDeath:
			TNT1 A 0 ACS_NamedExecute("Hit Marker Sound", 0, 1, 1, 6)
			PLSE ABCDE 4 Bright
			Stop
			
		XDeathM:
			TNT1 A 0 ACS_NamedExecute("Hit Marker Sound", 0, 1, 0, 6)
			Goto DeathCont		  
			
		DeathCont:
			TNT1 A 0 Bright A_SetDamageType("PlasmaJump")
			TNT1 A 0 Bright A_Explode(25, 64)
			TNT1 A 0 Bright A_SetDamageType("Normal")
			PLSE ABCDE 4 Bright
			Stop
	}
}

// Cell Pack 50
ACTOR CellPack50 : Cell 25028
{
	Height 25
	Radius 20
	Inventory.PickupMessage "Picked up cells."
	Inventory.Amount 50
	States
	{
		Spawn:
			CELB A -1
			Stop
	}
}