// Shotgun
ACTOR ShotgunE : Shotgun
{
	AttackSound "weapons/shotgf"
	Decal BulletChip
	Weapon.SlotNumber 3
	Weapon.SlotPriority 0
	Weapon.AmmoUse 1
	Obituary "%k \cc> \ciShotgun \cc> %o"
	-DROPPED
	Inventory.RespawnTics 125
	States
	{
		Deselect:
			SHTG A 0 A_Lower
			SHTG A 0 A_Lower
			SHTG A 1 A_Lower
			Loop	  

		Select:
			SHTG A 0 A_StopSound(7)
			SHTG A 0 A_PlaySound("wpncg", 7, 1, 0, 1.2)
			
		SelectLoop:
			SHTG A 0 A_Raise
			SHTG A 0 A_Raise
			SHTG A 1 A_Raise
			Loop

		Fire:
			TNT1 A 0 ACS_NamedExecuteAlways("Register Weapon Shot", 0, 3)
			SHTG A 3
			SHTG A 0 A_FireBullets (4.0, 3.0, 10, (8), "BulletPuff", FBF_USEAMMO | FBF_NORANDOM)
			SHTG A 7 A_GunFlash
			SHTG BC 5
			SHTG D 4
			SHTG CB 5
			SHTG A 3
			SHTG A 7 A_ReFire
			Goto Ready

		Spawn:
			TNT1 A 0
			TNT1 A 0 ACS_NamedExecuteAlways("Begin_Rotate")
			SHOT A 10
			Loop

		Rotate:
			TNT1 A 0
			TNT1 A 0 ACS_NamedExecuteAlways("End_Rotate")
			RTSG A 3 A_SetAngle(angle + 22.5)
			Loop

		NoRotate:
			TNT1 A 0 ACS_NamedExecuteAlways("Begin_Rotate")
			SHOT A 3
			Loop
	}
}

ACTOR PShotgun : CustomInventory replaces Shotgun
{
	+INVENTORY.IGNORESKILL
	+INVENTORY.ALWAYSPICKUP
	-DROPPED
	Inventory.RespawnTics 125
	Inventory.PickupMessage "$GOTSHOTGUN"
	Inventory.PickupSound "misc/w_pkup"
	States
	{
		Spawn:
			TNT1 A 0
			TNT1 A 0 ACS_NamedExecuteAlways("Begin_Rotate")
			SHOT A 10
			Loop
			
		Pickup:
			TNT1 A 0
			TNT1 A 0 A_JumpIfInventory("SuperShotgunE", 1, "Repickup")
			TNT1 A 0 A_JumpIfInventory("ShotgunE", 1, "Repickup")	
			TNT1 A 0 A_GiveInventory("ShotgunE", 1)
			TNT1 A 0 A_TakeInventory("Shell", 999)
			TNT1 A 0 ACS_NamedExecuteAlways("Give Weapon Ammo", 0, 3, 10)
			Stop
		Repickup:
			TNT1 A 0
			TNT1 A 0 ACS_NamedExecuteAlways("Give Weapon Ammo", 0, 3, 5)
			TNT1 A 0 A_JumpIfInventory("ShotgunE", 1, 3)
			TNT1 A 0 A_GiveInventory("ShotgunE", 1)
			TNT1 A 0 A_TakeInventory("Shell", 8)
			TNT1 A 0
			Stop
	}
}

// Dropped Shotgun
ACTOR DShotgunE : PShotgun
{
	States
	{
		Spawn:
			SHOT A 175
		SpawnLoop:
			SHOT A 7 A_FadeOut
			Loop
	}
}

// Shell Pack 10
ACTOR ShellPack10 : Shell 25029
{
	Height 25
	Radius 20
	Inventory.PickupMessage "Picked up shells."
	Inventory.Amount 10
	States
	{
		Spawn:
			SHLB A -1
			Stop
	}
}